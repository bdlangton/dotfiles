" Toggle the location and quickfix windows.
nnoremap <silent> <leader>cl :call ToggleList("Location List", 'l')<CR>
nnoremap <silent> <leader>ce :call ToggleList("Quickfix List", 'c')<CR>

" Open the file explorer in current working directory split vert or horiz.
nmap <Leader>- :sp.<CR>
nmap <Leader>\ :vs.<CR>

" Zoom a vim pane (<leader>+), <leader>= to re-balance.
nnoremap <leader>+ :wincmd _<cr>:wincmd \|<cr>
nnoremap <leader>= :wincmd =<cr>

" Swap windows like tmux.
nmap <leader>{ <C-w>x
nmap <leader>} <C-w>x

" Close current window like tmux.
nmap <leader>x :hide<CR>

function! GetBufferList()
  redir =>buflist
  silent! ls!
  redir END
  return buflist
endfunction

function! ToggleList(bufname, pfx)
  let buflist = GetBufferList()
  for bufnum in map(filter(split(buflist, '\n'), 'v:val =~ "'.a:bufname.'"'), 'str2nr(matchstr(v:val, "\\d\\+"))')
    if bufwinnr(bufnum) != -1
      exec(a:pfx.'close')
      return
    endif
  endfor
  if a:pfx == 'l' && len(getloclist(0)) == 0
    echohl ErrorMsg
    echo "Location List is Empty."
    return
  endif
  let winnr = winnr()
  exec(a:pfx.'open')
  if winnr() != winnr
    wincmd p
  endif
endfunction

" vim:ft=vim

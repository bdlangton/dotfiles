" Prompt for a single input to pass to Drupal Console.
function! DrupalSingleInput(command, text)
  call inputsave()
  let input = input(a:text)
  call inputrestore()
  execute 'VtrSendCommandToRunner drupal' g:drush_alias a:command input
endfunction

" Prompt for a single input to pass to Drush.
function! DrushSingleInput(command, text)
  call inputsave()
  let input = input(a:text)
  call inputrestore()
  execute 'VtrSendCommandToRunner drush' g:drush_alias a:command input
endfunction

" Execute a drush command.
function! ExecuteDrushCommand(command)
  execute 'VtrSendCommandToRunner drush' g:drush_alias a:command
endfunction

" Prompt for a drush alias to use.
function! SetDrushAlias()
  call inputsave()
  let g:drush_alias = input('Use drush alias: ')
  call inputrestore()
  call EchoDrushAlias()
endfunction

" Echo the drush alias.
function! EchoDrushAlias()
  echohl String | echo "\rDrush alias set to: " . g:drush_alias | echohl None
endfunction

" Set default drush alias to blank.
let g:drush_alias = ''

" Drupal mappings to the vim-tmux-runner.
nnoremap <Leader>dcc :call ExecuteDrushCommand('cr')<CR>
nnoremap <Leader>dud :call ExecuteDrushCommand('updb -y')<CR>
nnoremap <Leader>dul :call ExecuteDrushCommand('uli')<CR>
nnoremap <Leader>dwd :call ExecuteDrushCommand('watchdog-show')<CR>
nnoremap <Leader>dce :call ExecuteDrushCommand('cex sync -y')<CR>
nnoremap <Leader>dci :call ExecuteDrushCommand('cim sync -y')<CR>
nnoremap <Leader>dmi :call DrushSingleInput('en -y', 'Enter module to install: ')<CR>
nnoremap <Leader>dmu :call DrushSingleInput('pmu -y', 'Enter module to uninstall: ')<CR>
nnoremap <Leader>dsa :call SetDrushAlias()<CR>
nnoremap <Leader>dsl :call EchoDrushAlias()<CR>
nnoremap <Leader>drc :call DrushSingleInput('', 'Enter the drush command: ')<CR>

autocmd BufRead,BufNewFile *.theme setlocal filetype=php

" vim:ft=vim
